let newGame size = {
    maxPos = size * size - 1,
    pos x y = x + y * size,
    board = array (map $' ' [1 .. size * size]),
    full? () = not contains? ' ' board,
    hasWon player =
        m = size - 1;
        check from by = all (_ p = board[p * by + from] == player) [0 .. m];
        line i = check i size or check (i * size) 1;
        check 0 (m + 2) or check m m or any line [0 .. m],
    get show () =
        cell n =
            " | " ^ board[n] ^ if n % size == size - 1 then " |\n" else "" end;
        strJoin "" (map cell [0 .. maxPos]),
    let check me op =
        if hasWon me then
            {score = 1, pos = -1}
        elif hasWon op then
            {score = -1, pos = -1}
        elif full? () then
            {score = 0, pos = -1}
        else
            (tryPos best pos =
                if pos > maxPos or best.score > 0 then
                    best
                elif board[pos] != ' ' then
                    tryPos best (pos + 1)
                else
                    board[pos] := me;
                    res = -(check op me).score;
                    board[pos] := ' ';
                    if res > best.score then
                        tryPos {score = res, pos} (pos + 1)
                    else
                        tryPos best (pos + 1)
                    end
                end) {score = -2, pos = -1} 0
        end,
    let putBest me =
        board[(check me if me == 'O' then 'X' else 'O' end).pos] := me,
};

let game = newGame 3;
let computer = 'X';
let human = 'O';
(play humanMove =
    if game.hasWon computer then
        println "Game over. Computer won."
    elif game.hasWon human then
        println "You won! Impossible!"
    elif game.full? () then
        println "Draw."
    elif humanMove then
        print "${game.show}move xy> ";
        move = number (readln ());
        move = game.pos (move div 10 - 1) (move % 10 - 1);
        if move < 0 or move > game.maxPos or game.board[move] != ' ' then
            println "Illegal move. You'll be shot.";
            play true
        else
            game.board[move] := human;
            play false
        end
    else
        game.putBest computer;
        play true
    end) true;
print game.show;
