let script = map do v: { like = like (head v) . strLower,
                     replies = array (tail v) } done
   [['(father|mother|brother|sister)', "Tell me about your %."],
    [' (?:am|i''m) +(.*) $', "Why are you %?"],
    [' i +(.*) $', "Why do you %?", "Always it's about you..."],
    [' i .* want ', "Yes, you want", "Haha...", "Are you sure?",
                    "Maybe you shouldn't."],
    [' you +(.*) $', "We're talking about you, not me.",
                     "Please don't be so personal.",
                     "I'm your master",
                     "You don't want to know that...",
                     "I'm a worst nightmare you've seen."],
    [' hello| hi ', "Nice to see you.", "Hello...",
                     "What has happened to you?"],
    [' stop', "No, I won't", "Stop what?"],
    [' no', "No means yes..."],
    [' hell', "Hell is interesting",
              "Would you like to be in hell?"],
    [' really', "Yes, really..."],
    [' what', "You know what.", "You know very well...",
              "Do you want to know that?"],
    [' why', "Why? Why are you so pathetic?",
             "I don't know, why...",
             "Because you suck.",
             "Why not?"],
    [' why.*you', "Because I can"]];

let defaults =
    ["That's very interesting. Do go on.",
     "Tell me more.",
     "I'm not sure that I understand you fully.",
     "Please elaborate that.",
     "Can you elaborate on that?"];

let oneOf a = a[randomInt (length a)];

let answer s pat =
   (let r = pat.like s ();
    if empty? r then
        None ()
    else
        reply = oneOf pat.replies;
        Some if length r <= 1 then
            reply
        else
            strReplace '%' r[1] reply
        end
    end);

var s = "";

(print "? ";
 s := readln();
 defined? s) loop
   (s = " ${s} ";
    replies =  script |> map (s |> answer) |> unwrap;
    replies = if replies == [] then defaults else replies end;
    print "${oneOf (array replies)}\n")
